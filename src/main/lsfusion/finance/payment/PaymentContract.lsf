MODULE PaymentContract;

REQUIRE Payment, Contract;

NAMESPACE Payment;

contract = DATA Contract(Payment);
idContract 'Код договора' (Payment a) = id(contract(a));
numberContract 'Номер договора' (Payment a) = number(contract(a)) IN id;
nameContract 'Наименование договора' (Payment a) = name(contract(a)) IN id;
dateContract 'Дата договора' (Payment a) = date(contract(a));
typeContract (Payment p) = type(contract(p));
nameTypeContract 'Тип договора' (Payment p) = nameType(contract(p));
nameGroupContract 'Группа договора' (Payment p) = nameGroup(contract(p));

descriptionContract 'Договор' (Payment p) = description(contract(p));

CONSTRAINT contract(Payment p) AND legalEntity(p) AND NOT legalEntity(p) = legalEntity(contract(p)) CHECKED BY contract[Payment] MESSAGE 'Организация платежа должна совпадать с организацией договора';
CONSTRAINT contract(Payment p) AND company(p) AND NOT company(p) = company(contract(p)) CHECKED BY contract[Payment] MESSAGE 'Компания платежа должна совпадать с компанией договора';

sumContract 'Сумма в валюте договора' (Payment p) = round2(sum(p) * OVERRIDE rateOn(defaultTypeExchange(currency(contract(p))), currency(p), date(p)), 1) MATERIALIZED;

paid 'Оплачено' (Contract c) = GROUP SUM sumContract(Payment p) BY contract(p);
paidCurrency 'Оплачено в валюте' (Contract c) = GROUP SUM sumCurrency(Payment p) BY contract(p);
dateLastPaid 'Дата последней оплаты' (Contract c) = GROUP LAST date(Payment p) ORDER date(p), p BY contract(p);
paid 'Оплачено' (Contract c, DATE dt) = GROUP SUM sumContract(Payment p) IF date(p) <= dt BY contract(p);

paid(Contract c) += NUMERIC[16,5](paid(c));
paidCurrency(Contract c) += NUMERIC[16,5](paidCurrency(c));
dateLastPaid(Contract c) += dateLastPaid(c);

EXTEND FORM payment PROPERTIES (o) numberContract;

DESIGN payment {
    docs {
        MOVE PROPERTY (numberContract(o));
    }
}

EXTEND FORM payments 
    PROPERTIES (o) READONLY numberContract, nameTypeContract, nameGroupContract
    ORDERS numberContract(o)
;
EXTEND FORM dialogPayments 
    PROPERTIES (o) READONLY numberContract, nameTypeContract, nameGroupContract
    ORDERS numberContract(o)
;

EXTEND FORM dialogContracts 
    PROPERTIES (o) paidCurrency AFTER sumCurrency(o), paid AFTER sum(o)
    PROPERTIES (o) dateLastPaid AFTER paid(o);

EXTEND FORM contract
    OBJECTS p = Payment
    PROPERTIES (p) READONLY id, number, date, year, monthNumber, nameType, sum, shortNameCurrency, sumCurrency, note
    ORDERS date(p)
    FILTERS contract(p) == o
;

DESIGN contract {
    details {
        MOVE BOX (p) { caption = 'Платежи'; }
    }
}