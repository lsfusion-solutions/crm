MODULE CustomerRole;

REQUIRE Customer, Role, Employee;

in 'Вкл.' = DATA BOOLEAN (Customer, Employee, Role);
in (Customer c, Employee e) = GROUP SUM 1 IF in(c, e, Role r);

roles 'Роли' (Customer c, Employee e) = GROUP CONCAT name(Role r) IF in(c, e, r), ', ' ORDER name(r) CHARWIDTH 20;

FORM dialogCustomerRoles 'Роли'
    OBJECTS c = Customer PANEL 
    OBJECTS e = Employee PANEL 
    OBJECTS r = Role
    PROPERTIES in(c, e, r), name(r) READONLY 
    FILTERGROUP in FILTER 'Отмеченные' in(c, e, r)
;

in 'Вкл.' = DATA LOCAL BOOLEAN (Employee);
in 'Вкл.' = DATA LOCAL BOOLEAN (Role, Employee);

FORM dialogCustomerEmployees 'Сотрудники'
    OBJECTS c = Customer PANEL 
    OBJECTS e = Employee
    PROPERTIES in(e)
    PROPERTIES(e) READONLY name, id, number, namePosition, nameLegalEntity
    ORDERS name(e)
    FILTERS NOT in(c, e)
    
    OBJECTS r = Role
    PROPERTIES in(r, e), name(r) READONLY 
;

DESIGN dialogCustomerEmployees {
    size = (600, 600);
}

addEmployee 'Добавить' (Customer c) {
    DIALOG dialogCustomerEmployees OBJECTS e INPUT DO {
        in(c, Employee ee, Role r) <- TRUE WHERE in(r, ee);
    }
} IMAGE 'add.png';

EXTEND FORM customer
    OBJECTS e = Employee
    PROPERTIES name(e) READONLY, roles(o, e) ON CHANGE { SHOW dialogCustomerRoles OBJECTS c = o, e = e; }
    PROPERTIES DRAW e TOOLBAR addEmployee(o)
    FILTERS in(o, e)
    ORDERS name(e)
;
