MODULE LegalEntity;

REQUIRE Customer, Currency, Address;

NAMESPACE LegalEntity;

CLASS LegalEntity 'Организация';
TABLE legalEntity (LegalEntity);

id 'Код' = DATA STRING[15] (LegalEntity) INDEXED CHARWIDTH 5 IN id;
legalEntity (id) = GROUP AGGR LegalEntity le BY id (le);
isCompany 'Компания' = DATA BOOLEAN (LegalEntity);

extId 'Код для экспорта' = DATA STRING[15](LegalEntity) IN id;
legalEntityEx (id) = GROUP AGGR LegalEntity le BY extId (le);

name 'Наименование' = DATA ISTRING[250] (LegalEntity) CHARWIDTH 20 IN id;
fullName 'Полное наименование' = DATA ISTRING[250] (LegalEntity) CHARWIDTH 50;
overName 'Наименование' (LegalEntity l) = OVERRIDE fullName(l), name(l);

legalEntity = DATA LegalEntity(Address);
nameLegalEntity 'Организация' (Address a) = name(legalEntity(a));
physicalAddress (LegalEntity l) = GROUP LAST Address a ORDER a IF in(a, AddressType.physical) BY legalEntity(a);
namePhysicalAddress 'Физ. адрес' (LegalEntity l) = name(physicalAddress(l));
legalAddress (LegalEntity l) = GROUP LAST Address a ORDER a IF in(a, AddressType.legal) BY legalEntity(a);
nameLegalAddress 'Юр. адрес' (LegalEntity l) = name(legalAddress(l));

customer = DATA Customer(LegalEntity) INDEXED;
idCustomer 'Код клиента' (LegalEntity l) = id(customer(l));
nameCustomer 'Клиент' (LegalEntity l) = name(customer(l)) IN id;
nameStatusCustomer 'Статус' (LegalEntity l) = nameStatus(customer(l));

merge (Customer from, Customer to) + { customer(LegalEntity l) <- to WHERE customer(l) = from; }

CLASS Bank 'Банк';
TABLE bank (Bank);

id 'Код' = DATA STRING[15] (Bank) MATERIALIZED INDEXED CHARWIDTH 5 IN id;
bank (id) = GROUP AGGR Bank b BY id (b);

number 'Код банка' = DATA STRING[10](Bank) CHARWIDTH 5 IN id;
name 'Наименование' = DATA ISTRING[100] (Bank) CHARWIDTH 15 IN id;
postcode 'Почтовый индекс' = DATA STRING[50](Bank) CHARWIDTH 6;
address 'Адрес' = DATA STRING[250](Bank) CHARWIDTH 20;
mfo 'МФО' = DATA STRING[11] (Bank);

FORM bank 'Банк'
    OBJECTS o = Bank PANEL
    PROPERTIES(o) id, number, name, address, mfo
    
    EDIT Bank OBJECT o
;

FORM banks 'Банки'
    OBJECTS o = Bank
    PROPERTIES(o) READONLYIF isReadonly() id, number, name, address, mfo
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDERS name(o)
;

@extendFormEditable(banks);

FORM dialogBanks 'Банки'
    OBJECTS o = Bank
    PROPERTIES(o) READONLY id, number, name, address, mfo
    ORDERS name(o)
    
    LIST Bank OBJECT o
;

NAVIGATOR {
    customerFolder {
        NEW banks;
    }
}

CLASS Account 'Расчетный счет';
TABLE account (Account);

number 'Номер расчетного счета'  = DATA STRING[50] (Account) IN id CHARWIDTH 28 NOFLEX;
account (number) = GROUP MAX Account account BY number(account);

note 'Примечание'  = DATA ISTRING[100] (Account);
rate 'Процентная ставка' = DATA NUMERIC[6,2](Account);
dateTo 'Дата по' = DATA DATE (Account);
active 'Активный' (Account a) = dateTo(a) >= currentDate() OR NOT dateTo(a);

bank = DATA Bank (Account);
nameBank 'Наименование банка' (Account account) = name(bank(account)) IN id;
addressBank 'Адрес банка' (Account account) = address(bank(account));
MFOBank 'Код BIC банка' (Account account) = mfo(bank(account));

legalEntity = DATA LegalEntity (Account) NONULL DELETE AUTOSET;
nameLegalEntity 'Организация' (Account account) = name(legalEntity(account));
nameCustomer 'Организация' (Account account) = name(customer(legalEntity(account)));

account (number, legalEntity) = GROUP MAX Account account BY number(account), legalEntity(account);

defaultAccount(legalEntity) = GROUP MAX Account account IF active(account) BY legalEntity(account);

userAccount = DATA Account (LegalEntity);
CONSTRAINT legalEntity(userAccount(LegalEntity legalEntity)) != legalEntity CHECKED MESSAGE 'ошибка: Р/сч. по умолчанию должен соответствовать р/сч. Ю.Л.';

account (LegalEntity legalEntity) =  OVERRIDE userAccount(legalEntity), defaultAccount(legalEntity) MATERIALIZED INDEXED;
numberAccount 'Основной р/сч.' (LegalEntity legalEntity) = number(account(legalEntity));
nameBank 'Банк' (LegalEntity legalEntity) = nameBank(account(legalEntity));
addressBank 'Адрес банка' (LegalEntity legalEntity) = addressBank(account(legalEntity));
mfoBank 'Код BIC банка' (LegalEntity legalEntity) = MFOBank(account(legalEntity));
numberBank 'Код банка' (LegalEntity l) = number(bank(account(l)));
postcodeBank 'Почтовый индекс банка' (LegalEntity l) = postcode(bank(account(l)));

equals 'Основной' (LegalEntity legalEntity, Account account) = userAccount(legalEntity) == account;

accountID 'Расчетный счет по номеру' (number, legalEntityId) = GROUP AGGR Account account WHERE account IS Account BY number(account), id(legalEntity(account));
legalEntityAccount (STRING[20] number) = legalEntity(account(number)); 

currency = DATA Currency (Account);
nameCurrency 'Валюта счета' = name(currency(Account account));

FORM dialogAccounts 'Счета'
    OBJECTS a = Account
    PROPERTIES (a) READONLY number, nameCurrency, nameBank, addressBank, MFOBank, nameLegalEntity, nameCustomer, note, rate, dateTo
    FILTERGROUP active FILTER 'Активные' active(a) 'ctrl F10' DEFAULT 
    
    LIST Account OBJECT a
;

EXTEND FORM customer PROPERTIES nameLegalEntity(a) AFTER types(a);

WHEN LOCAL SETCHANGED(customer(legalEntity(Address a))) DO {
    customer(a) <- customer(legalEntity(a));
}

CONSTRAINT legalEntity(Address a) AND customer(a) AND NOT customer(legalEntity(a)) == customer(a) CHECKED BY legalEntity[Address], customer[Address] 
    MESSAGE 'Организация адреса не соответствует организации клиента адреса';

FORM legalEntity 'Организация'
    OBJECTS o = LegalEntity PANEL
    PROPERTIES(o) id, extId, name, fullName, nameCustomer, nameBank, numberAccount, isCompany
    ORDERS name(o)
    
    OBJECTS a=Account
    PROPERTIES(a)  number, nameCurrency, nameBank, addressBank, MFOBank, note, rate, dateTo, NEW, DELETE GRID
    FILTERS legalEntity(a) == o
    PROPERTIES equals(o, a)
    FILTERGROUP active FILTER 'Активные' active(a) 'ctrl F10' DEFAULT 
    
    OBJECTS d = Address
    PROPERTIES (d) types ON CHANGE changeAddressTypes(d), postcode, region, district, locality, street, houseNumber, officeNumber, extra, NEW, DELETE 
    ORDERS types(d) DESC 
    FILTERS legalEntity(d) == o
    
    EDIT LegalEntity OBJECT o
;

DESIGN legalEntity {
    OBJECTS {
        NEW main FIRST {
            alignment = STRETCH;
            horizontal = TRUE;
            caption = 'Основные параметры';
            MOVE PROPERTY (id(o));
            MOVE PROPERTY (name(o));
            MOVE PROPERTY (isCompany(o));
            MOVE PROPERTY (extId(o));
        }
        NEW row AFTER main {
            alignment = STRETCH;
            NEW client {
                alignment = STRETCH;
                caption = 'Клиент';
                horizontal = TRUE;
                MOVE PROPERTY (nameCustomer(o)) {charWidth = 30; }
            }
            NEW bank {
                alignment = STRETCH;
                caption = 'Реквизиты';
                horizontal = TRUE;
                MOVE PROPERTY (numberAccount(o));
                MOVE PROPERTY (nameBank(o)) { charWidth = 30; }
            }
            NEW documents {
                alignment = STRETCH;
                caption = 'Документы';
                horizontal = TRUE;
                MOVE PROPERTY(fullName(o));
            }
        }
        NEW tab {
            fill = 1;
            tabbed = TRUE;
            MOVE BOX (a);
            MOVE BOX (d);
        }
    }
}

FORM legalEntities 'Организации'
    OBJECTS o = LegalEntity
    PROPERTIES(o) READONLYIF isReadonly() id, extId, name, fullName, nameCustomer, nameStatusCustomer, nameLegalAddress, numberBank, 
                             nameBank, addressBank, mfoBank, numberAccount, isCompany
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ORDERS name(o)
;

@extendFormEditable(legalEntities);

FORM dialogLegalEntities 'Организации'
    OBJECTS o = LegalEntity
    PROPERTIES(o) READONLY id, extId, name, fullName, nameCustomer, nameStatusCustomer, nameLegalAddress, numberBank, nameBank, addressBank, mfoBank, numberAccount, isCompany
    PROPERTIES(o) NEWSESSION EDIT
    ORDERS name(o)
    
    LIST LegalEntity OBJECT o
;

NAVIGATOR {
    customerFolder {
        NEW legalEntities;
    }
}

skipReceipt 'Не учитывать в доходах' = DATA BOOLEAN (LegalEntity);

EXTEND FORM legalEntity PROPERTIES skipReceipt(o);
DESIGN legalEntity {
    main {
        MOVE PROPERTY (skipReceipt(o));
    }   
}