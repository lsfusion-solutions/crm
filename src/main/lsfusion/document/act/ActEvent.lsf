MODULE ActEvent;

REQUIRE Act, AgreementEvent, DocDashboard;

NAMESPACE Act;

act = DATA Act(Event);
idAct 'Код акта' (Event e) = id(act(e));
numberAct 'Акт' (Event e) = number(act(e));

EXTEND FORM events PROPERTIES numberAct(o);
EXTEND FORM event PROPERTIES numberAct(o);

DESIGN event {
    docs {
        MOVE PROPERTY (numberAct(o));
    }
}

addEvent (Event e, Act a) + {
    act(e) <- a;
}

WHEN LOCAL SETCHANGED (act(Event e)) AND NOT CHANGED (contract(e)) DO {
    contract(e) <- contract(act(e));
    agreement(e) <- agreement(act(e));
}

CONSTRAINT contract(Event e) AND act(e) AND NOT contract(act(e)) == contract(e) CHECKED BY act[Event] MESSAGE 'Акт не соответствует договору';
CONSTRAINT agreement(Event e) AND act(e) AND NOT agreement(act(e)) == agreement(e) CHECKED BY act[Event] MESSAGE 'Акт не соответствует соглашению';
CONSTRAINT act(Event e) AND legalEntity(e) AND NOT legalEntity(e) == legalEntity(act(e)) MESSAGE 'Акт не соответствует организации';

EXTEND FORM docDashboard
    EXTEND FILTERGROUP event FILTER 'По документу' contract(e) == d OR agreement(e) == d OR act(e) == d DEFAULT
; 

document (Event e) += OVERRIDE act(e), agreement(e), contract(e);