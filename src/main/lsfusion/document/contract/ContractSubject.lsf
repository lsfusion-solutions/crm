MODULE ContractSubject;

REQUIRE Contract, Agreement, DocFile;

NAMESPACE Contract;

CLASS SubjectType 'Тип предмета договора';
TABLE subjectType (SubjectType);

id 'Код' = DATA STRING[15] (SubjectType) CHARWIDTH 5 IN id;
subjectType (id) = GROUP AGGR SubjectType c BY id (c);

name 'Наименование' = DATA ISTRING[100] (SubjectType) CHARWIDTH 15;

FORM subjectType 'Тип предмета договора'
    OBJECTS o = SubjectType PANEL
    PROPERTIES(o) id, name
    
    EDIT SubjectType OBJECT o
;

FORM subjectTypes 'Типы предмета договора'
    OBJECTS o = SubjectType
    PROPERTIES(o) READONLY id, name
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST SubjectType OBJECT o
;

CLASS Subject 'Предмет договора';
TABLE subject (Subject);

name 'Наименование' = DATA ISTRING[100] (Subject) CHARWIDTH 15;

type = DATA SubjectType(Subject);
nameType 'Тип предмета договора' (Subject s) = name(type(s));
date 'Дата' = DATA DATE (Subject);
quantity 'Кол-во' = DATA NUMERIC[14,2] (Subject);
price 'Цена' = DATA NUMERIC[16,5](Subject);
note 'Примечание' = DATA TEXT (Subject);

sum 'Сумма' (Subject s) = DATA NUMERIC[16,5](Subject);

WHEN LOCAL (CHANGED (price(Subject s)) OR CHANGED (quantity(s))) AND NOT CHANGED (sum(s)) DO {
    sum(s) <- quantity(s) * price(s);
}

CLASS ContractSubject 'Предмет договора' : Subject;

contract = DATA Contract(ContractSubject) NONULL DELETE;
numberContract 'Номер договора' (ContractSubject s) = number(contract(s));
nameCustomerContract 'Клиент договора' (ContractSubject s) = nameCustomer(contract(s));

CLASS AgreementSubject 'Предмет договора' : Subject;

agreement = DATA Agreement(AgreementSubject) NONULL DELETE;
numberAgreement 'Номер соглашения' (AgreementSubject s) = number(agreement(s));
nameTypeAgreement 'Тип соглашения' (AgreementSubject s) = nameType(agreement(s));
nameCustomerAgreement 'Клиент договора соглашения' (AgreementSubject s) = nameCustomer(agreement(s));
contractAgreement (AgreementSubject s) = contract(agreement(s));

FORM subject 'Предмет договора'
    OBJECTS o = Subject PANEL
    PROPERTIES(o) name, nameType, date, numberContract, quantity, price, sum, note
    
    EDIT Subject OBJECT o
;

FORM subjects 'Предметы договора'
    OBJECTS o = Subject
    PROPERTIES(o) READONLY name, nameType, date, numberContract, nameCustomerContract, quantity, price, sum, note PANEL 
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;

DESIGN subjects {
    OBJECTS {
        MOVE BOX (o) { fill = 4; }
        NEW note {
            fill = 1;
            caption = 'Примечание';
            MOVE  PROPERTY (note(o)) {
                caption = '';
                fill = 1;
            }
        }
    }
}

FORM dialogSubjects 'Предметы договора'
    OBJECTS o = Subject
    PROPERTIES(o) READONLY name, nameType, date, numberContract, nameCustomerContract, quantity, price, sum, note 
    
    LIST Subject OBJECT o
;

NAVIGATOR {
    contractFolder {
        NEW subjects;
        NEW subjectTypes;
    }
}

EXTEND FORM agreement 
    OBJECTS s = AgreementSubject
    PROPERTIES (s) name, nameType, date, quantity, price, sum, note PANEL, NEW, DELETE 
    FILTERS agreement(s) == o
;

DESIGN agreement {
    details {
        NEW subjects {
            caption = 'Предметы договора';
            horizontal = TRUE;
            MOVE BOX (s) { fill = 4; }
            NEW note {
                fill = 1;
                caption = 'Примечание';
                MOVE  PROPERTY (note(s)) {
                    caption = '';
                    fill = 1;
                }
            }
        }
    }
}

sum 'Сумма' (SubjectType t, Contract c) = GROUP SUM sum(Subject s) IF contract(s) == c OR contractAgreement(s) == c BY type(s);

EXTEND FORM contract 
    OBJECTS s = ContractSubject
    PROPERTIES (s) name, nameType, date, quantity, price, sum, note PANEL, NEW, DELETE 
    FILTERS contract(s) == o
    
    OBJECTS as = AgreementSubject
    PROPERTIES (as) READONLY numberAgreement, nameTypeAgreement, name, nameType, date, quantity, price, sum, note PANEL
    FILTERS contractAgreement(as) == o
    
    OBJECTS t = SubjectType
    PROPERTIES READONLY name(t), sum(t, o)
    FILTERS sum(t, o)
;

DESIGN contract {
    details {
        NEW subjects {
            horizontal = TRUE;
            fill = 1;
            caption = 'Предметы договора';
            NEW summary {
                caption = 'Итого';
                MOVE BOX (t);
                fill = 1;
            }
            NEW tabSubjects {
                tabbed = TRUE;
                fill = 4;
                NEW contractSubjects {
                    horizontal = TRUE;
                    caption = 'По договору';
                    MOVE BOX (s) { fill = 4; }
                    NEW note {
                        fill = 1;
                        caption = 'Примечание';
                        MOVE  PROPERTY (note(s)) {
                            caption = '';
                            fill = 1;
                        }
                    }
                }
                NEW agreementSubjects {
                    horizontal = TRUE;
                    caption = 'По соглашениям';
                    MOVE BOX (as) { fill = 4; }
                    NEW noteA {
                        fill = 1;
                        caption = 'Примечание';
                        MOVE  PROPERTY (note(as)) {
                            caption = '';
                            fill = 1;
                        }
                    }
                }
            }
        }
    }
}