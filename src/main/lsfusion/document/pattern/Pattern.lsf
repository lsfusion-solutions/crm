MODULE Pattern;

REQUIRE Utils, Word;

NAMESPACE Pattern;

CLASS Pattern 'Заготовка';
TABLE pattern (Pattern);

text 'Текст' = DATA TEXT (Pattern) CHARWIDTH 50;
note 'Примечание' = DATA TEXT (Pattern) CHARWIDTH 20;
textString 'Текст' (Pattern p) = ISTRING[1000](text(p)) CHARWIDTH 20;
noteString 'Примечание' (Pattern p) = ISTRING[1000](note(p)) CHARWIDTH 20;
order 'Номер' = DATA INTEGER (Pattern);

FORM pattern 'Заготовка'
    OBJECTS o = Pattern PANEL
    PROPERTIES(o) order, note, text 
    
    EDIT Pattern OBJECT o
;

DESIGN pattern {
    BOX (o) {
        fill = 1;
        NEW note {
            height = 100;
            alignment = STRETCH;
            caption = 'Примечание';
            MOVE PROPERTY (note(o)) { caption = ''; fill = 1; }
        }
        NEW text {
            fill = 1;
            caption = 'Текст';
            MOVE PROPERTY (text(o)) { caption = ''; fill = 1; }
        }
    }
}

FORM patterns 'Заготовки'
    OBJECTS p = Pattern
    PROPERTIES(p) READONLYIF isReadonly() order, note, text
    PROPERTIES(p) SHOWIF isReadonly() NEWSESSION NEW, EDIT, DELETE
    PROPERTIES(p) SHOWIF isEditable() new = NEW, delete = DELETE
    ORDERS order(p)
;

@extendFormEditable(patterns);

FORM dialogPatterns 'Заготовки'
    OBJECTS p = Pattern
    PROPERTIES(p) READONLY order, note, text
    ORDERS order(p)
    
    LIST Pattern OBJECT p
;

NAVIGATOR {
    templatesNavigator {
        NEW patterns;
    }
}