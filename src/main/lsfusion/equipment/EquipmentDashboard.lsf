MODULE EquipmentDashboard;

REQUIRE EquipmentLicense, Dashboard, Utils;

NAMESPACE Equipment;

filterCustomer = DATA LOCAL NESTED Customer();
nameFilterCustomer 'Клиент' () = name(filterCustomer());

filterContract = DATA LOCAL NESTED Contract(SubjectType);
nameFilterContract 'Договор' (SubjectType t) = description(filterContract(t));

CONSTRAINT filterContract(SubjectType t) AND NOT in(filterContract(t), t) CHECKED BY filterContract 
    MESSAGE 'В выбранном договоре нет предметов договора с выбранным типом';
    
CONSTRAINT filterContract(SubjectType t) AND NOT customer(filterContract(t)) == filterCustomer() CHECKED BY filterContract
    MESSAGE 'Клиент выбранного договора не соответсвует клиенту';
    
addEquipment 'Добавить' (SubjectType t) {
    NEWSESSION NESTED (filterContract) NEW e = Equipment {
        subjectType(e) <- t;
        contract(e) <- filterContract(t);
        customer(e) <- filterCustomer();
        SHOW equipment OBJECTS o = e DOCKED;
    }
} IMAGE 'add.png';

quantitySerial 'Кол-во серийных номеров' (SubjectType t) = 
    GROUP SUM 1 IF (contract(equipment(EquipmentSerial s)) == filterContract(t) OR (NOT filterContract(t) AND subjectType(equipment(s)) = t)) 
               AND (customer(equipment(s)) == filterCustomer() OR NOT filterCustomer());

FORM equipmentDashboard 'Оборудование'
    OBJECTS t = SubjectType PANEL 
    PROPERTIES name(t) SELECTOR, nameFilterContract(t), nameFilterCustomer() SELECTOR, quantitySerial(t) READONLY 
    
    OBJECTS e = Equipment
    PROPERTIES (e) READONLY note, quantitySerial, nameCreatedUser, createdDate, createdTime, nameCustomer SHOWIF NOT filterCustomer(), nameContract, nameLegalEntityContract, nameAgreement
    PROPERTIES DRAW e TOOLBAR generateLicense(e), downloadFile(e) SHOWIF TRUE IF file(e), addEquipment(t) 
    PROPERTIES (e) NEWSESSION EDIT, DELETE SHOWIF TRUE IF NOT file(e)
    FILTERS subjectType(e) == t, customer(e) == filterCustomer() OR NOT filterCustomer(), contract(e) == filterContract(t) OR NOT filterContract(t)
    
    OBJECTS n = EquipmentSerial
    PROPERTIES (n) READONLY number
    FILTERS equipment(n) == e
    
    OBJECTS es = EquipmentSerial
    PROPERTIES (es) READONLY noteEquipment, quantitySerialEquipment, nameCreatedUserEquipment, createdDateEquipment, 
                             createdTimeEquipment, nameSubjectTypeEquipment, nameContractEquipment, nameLegalEntityContractEquipment, number
    FILTERS customer(equipment(es)) == filterCustomer() OR NOT filterCustomer()
    ORDERS createdDateEquipment(es), createdTimeEquipment(es)
    
    OBJECTS l = SerialNumberLog
    PROPERTIES (l) READONLY date, time, hostnameComputer, loginUser, nameContact, numberSerial
    FILTERS equipment(l) == e
    FILTERGROUP serial FILTER 'По серийному номеру' equipmentSerial(l) == n
    ORDERS date(l), time(l)

;

DESIGN equipmentDashboard {
    OBJECTS {
        NEW head FIRST {
            caption = 'Параметры';
            horizontal = TRUE;
            MOVE PROPERTY (name(t)) { caption = 'Тип предмета договора'; }
            MOVE PROPERTY (nameFilterCustomer()) { caption = 'Клиент'; }
            MOVE PROPERTY (nameFilterContract(t)) { caption = 'Договор'; }
            MOVE PROPERTY (quantitySerial(t));
        }
        NEW tab {
            tabbed = TRUE;
            fill = 1;
            NEW equip {
                caption = 'Оборудование';
                horizontal = TRUE;
                fill = 1;
                MOVE BOX (e) { fill = 2; }
                NEW serials { 
                    fill = 1;
                    MOVE BOX (n);
                    MOVE BOX (l) { caption = 'Лог запросов на получение лицензионного ключа'; }
                }
            }
            MOVE BOX (es) { caption = 'Серийные номера по клиенту'; }
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW equipmentDashboard;
    }
}