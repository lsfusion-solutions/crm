MODULE EventImport;

REQUIRE Event, ContractEvent, AgreementEvent, ActEvent;

NAMESPACE Event;

importEvents 'Импорт событий' (FILE f) {

    LOCAL idEvent = STRING[15](INTEGER);
    LOCAL idCustomer = STRING[15](INTEGER);
    LOCAL idLegalEntity = STRING[15](INTEGER);
    LOCAL idEmployee = STRING[15](INTEGER);
    LOCAL idContract = STRING[15](INTEGER);
    LOCAL idAgreement = STRING[15](INTEGER);
    LOCAL idAct = STRING[15](INTEGER);
    LOCAL type = STRING[50](INTEGER);
    LOCAL date = DATE (INTEGER);
    LOCAL time = STRING[5] (INTEGER);
    LOCAL idContact = STRING[15](INTEGER);
    LOCAL name = STRING[100](INTEGER);
    LOCAL text = TEXT(INTEGER);
    LOCAL dateNext = DATE (INTEGER);
    LOCAL add = TEXT(INTEGER);
    
    
    IMPORT XLS NOHEADER FROM f TO idEvent = B, name, type, idCustomer, idLegalEntity, idEmployee, idContact, date, time, 
                                  dateNext, text, add, idContract, idAgreement, idAct;
    
    time(INTEGER i) <- NULL WHERE NOT substr(time(i), 3, 1) == ':';
    
    FOR [GROUP SUM 1 IF INTEGER i > 1 BY trim(idEvent(i))](STRING[15] id) AND id != '' AND NOT event(id) DO NEW c = Event {
        id(c) <- id;
    }
    
    FOR Event c = event(trim(idEvent(INTEGER i))) AND trim(idEvent(i)) != '' AND i > 1 DO {
        
        customer(c) <- OVERRIDE customer(legalEntity(trim(idLegalEntity(i)))), customer(trim(idCustomer(i)));
        legalEntity(c) <- legalEntity(trim(idLegalEntity(i)));
        employee(c) <- GROUP MAX Employee e IF id(e) == idEmployee(i);
        date(c) <- date(i);
        time(c) <- TIME (time(i));
        contact(c) <- Contact.contact(idContact(i));
        name(c) <- trim(name(i));
        dateNext(c) <- dateNext(i);
        description(c) <- trim(text(i));
        addition(c) <- add(i);
        contract(c) <- contract(idContract(i));
        agreement(c) <- agreement(idAgreement(i));
        act(c) <- act(idAct(i));
    }
}

importEvents 'Импорт событий' () {
    NEWSESSION INPUT f = FILE DO {
        importEvents(f);
        APPLY;
    }
}

FORM exportEvents 'Экспорт событий'
    OBJECTS p = Event
    PROPERTIES (p) id, name, nameType, idCustomer, idLegalEntity, idEmployee, idContact, date, time, dateNext, 
                   description, addition, idContract, idAgreement, idAct
    ORDERS date(p)
;

exportEvents 'Экспорт событий' () {
    
    LOCAL file = FILE();
    
    PRINT exportEvents XLS TO file;
    WRITE CLIENT DIALOG file() TO 'События';
    open(file(), 'События');
}

EXTEND FORM migrationData PROPERTIES() exportEvents, importEvents;
